/*
 * Copyright 2013-2014 SmartBear Software
 * Copyright 2014-2015 The TestFX Contributors
 *
 * Licensed under the EUPL, Version 1.1 or - as soon they will be approved by the
 * European Commission - subsequent versions of the EUPL (the "Licence"); You may
 * not use this work except in compliance with the Licence.
 *
 * You may obtain a copy of the Licence at:
 * http://ec.europa.eu/idabc/eupl
 *
 * Unless required by applicable law or agreed to in writing, software distributed
 * under the Licence is distributed on an "AS IS" basis, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the Licence for the
 * specific language governing permissions and limitations under the Licence.
 */

dependencies {
    // use correct API internally depending on which Java version is being used
    if (usingJava9) {
        compile project(":testfx-internal-java9")
    } else {
        compile project(":testfx-internal-java8")
    }

    compile "org.hamcrest:hamcrest-core:1.3"
    compileOnly 'org.osgi:org.osgi.core:6.0.0'

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.12.0"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile project(":testfx-junit")
    if (usingJava9) {
        testCompile "org.testfx:openjfx-monocle:jdk-9+181"
    } else {
        testCompile "org.testfx:openjfx-monocle:8u76-b04"
    }
}


compileJava {
    if (usingJava9) {
        options.compilerArgs += [
                "--add-opens", "java.base/java.lang=ALL-UNNAMED",
                "--add-opens", "java.base/java.util=ALL-UNNAMED",
                "--add-exports", "javafx.graphics/com.sun.glass.ui=ALL-UNNAMED"
        ]

        javadoc {
            options.addStringOption('-add-exports', 'javafx.graphics/com.sun.glass.ui=ALL-UNNAMED')
        }
    }
}

compileTestJava {
    if (usingJava9) {
        options.compilerArgs += [
                "--add-opens", "org.testfx.internal/org.testfx.service.adapter=ALL-UNNAMED",
                "--add-opens", "java.base/java.lang=ALL-UNNAMED",
                "--add-opens", "java.base/java.util=ALL-UNNAMED",
                "--add-exports", "javafx.graphics/com.sun.glass.ui=ALL-UNNAMED"
        ]
    }
}

jar {
    manifest {
        instruction 'Export-Package', '*'
        instruction 'Bundle-Activator', 'org.testfx.osgi.Activator'
        instruction 'Require-Capability', 'org.testfx.osgi.versionadapter'
    }
}
