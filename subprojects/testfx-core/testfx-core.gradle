/*
 * Copyright 2013-2014 SmartBear Software
 * Copyright 2014-2015 The TestFX Contributors
 *
 * Licensed under the EUPL, Version 1.1 or - as soon they will be approved by the
 * European Commission - subsequent versions of the EUPL (the "Licence"); You may
 * not use this work except in compliance with the Licence.
 *
 * You may obtain a copy of the Licence at:
 * http://ec.europa.eu/idabc/eupl
 *
 * Unless required by applicable law or agreed to in writing, software distributed
 * under the Licence is distributed on an "AS IS" basis, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the Licence for the
 * specific language governing permissions and limitations under the Licence.
 */
ext.moduleName = 'org.testfx'


repositories {
    jcenter()
}

dependencies {
    // use correct API internally depending on which Java version is being used
    if (usingJava9) {
        compile project(":testfx-internal-java9")
    } else {
        compile project(":testfx-internal-java8")
    }

    compile "org.hamcrest:hamcrest-core:1.3"
    compileOnly 'org.osgi:org.osgi.core:6.0.0'

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.12.0"
    testCompile project(":testfx-junit")
    if (usingJava9) {
        testCompile "org.testfx:openjfx-monocle:jdk-9+181"
    } else {
        testCompile "org.testfx:openjfx-monocle:8u76-b04"
    }
}

sourceSets {
    if (!usingJava9) {
        main {
            java {
                exclude '**/module-info.java'
            }
        }
    }
}

compileJava {
    if (usingJava9) {
        inputs.property("moduleName", moduleName)
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
                    "--add-exports", "javafx.graphics/com.sun.glass.ui=org.testfx"
            ]
            classpath = files()
        }

        javadoc {
            exclude "**/module-info.java"
            options.addStringOption('-module-path', classpath.asPath)
            options.addStringOption('-add-exports', 'javafx.graphics/com.sun.glass.ui=org.testfx')
        }
    }
}

compileTestJava {
    if (usingJava9) {
        inputs.property("moduleName", moduleName)
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
                    '--add-modules', 'junit',
                    '--add-reads', "$moduleName=junit",
                    "--add-exports", "javafx.graphics/com.sun.glass.ui=org.testfx",
                    '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
            ]
            classpath = files()
        }
    }
}

jar {
    inputs.property("moduleName", moduleName)

    manifest {
        attributes('Automatic-Module-Name': moduleName)
        instruction 'Export-Package', '*'
        instruction 'Bundle-Activator', 'org.testfx.osgi.Activator'
        instruction 'Require-Capability', 'org.testfx.osgi.versionadapter'
    }
}
