/*
 * Copyright 2013-2014 SmartBear Software
 * Copyright 2014-2015 The TestFX Contributors
 *
 * Licensed under the EUPL, Version 1.1 or - as soon they will be approved by the
 * European Commission - subsequent versions of the EUPL (the "Licence"); You may
 * not use this work except in compliance with the Licence.
 *
 * You may obtain a copy of the Licence at:
 * http://ec.europa.eu/idabc/eupl
 *
 * Unless required by applicable law or agreed to in writing, software distributed
 * under the Licence is distributed on an "AS IS" basis, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the Licence for the
 * specific language governing permissions and limitations under the Licence.
 */

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

//-------------------------------------------------------------------------------------------------
// GRADLE PLUGINS.
//-------------------------------------------------------------------------------------------------

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.14.0"
    id "com.jfrog.bintray" version "1.8.0"
    id 'com.github.ben-manes.versions' version '0.17.0'
}

//-------------------------------------------------------------------------------------------------
// ROOT PROJECT.
//-------------------------------------------------------------------------------------------------

rootProject.with { project ->
    // provide assemble and clean tasks.
    apply(plugin: "base")

    // task to add gradle wrapper files.
    task("wrapper", type: Wrapper) {
        gradleVersion = '4.3.1'
        distributionUrl = "https://services.gradle.org/distributions/" +
            "gradle-${gradleVersion}-all.zip"
    }

    // task to print gradle and groovy versions.
    task("versions", group: "help").doLast {
        println "JAVA_OPTS: ${System.env.JAVA_OPTS}"
        println "JAVA_HOME: ${System.env.JAVA_HOME}"
        println "Java version: ${System.properties["java.version"]}"
        println "Gradle version: ${gradle.gradleVersion}"
        println "Groovy version: ${GroovySystem.version}"
        println "Included projects are: ${rootProject.childProjects.keySet()}"
    }

    ext {
        // add build time and date to project attributes.
        def buildTimeAndDate = new Date()
        buildDate = buildTimeAndDate.format("yyyy-MM-dd")
        buildTime = buildTimeAndDate.format("HH:mm:ss.SSSZ")

        // add java version and vendor to project attributes.
        javaVersion = System.properties["java.version"]

        usingJava9 = javaVersion.startsWith("9")
        javaSourceAndTargetCompilation = usingJava9 ? "9" : "1.8"

        if (usingJava9) {
            javadocLinks = [
                    "https://docs.oracle.com/javase/9/docs/api"
            ]
        } else {
            javadocLinks = [
                    "http://docs.oracle.com/javase/8/docs/api/",
                    "http://docs.oracle.com/javase/8/javafx/api/"
            ]
        }
    }
}
allprojects { project ->
    configurations {
        providedCompile
    }
}
//-------------------------------------------------------------------------------------------------
// EACH SUBPROJECT.
//-------------------------------------------------------------------------------------------------

subprojects { subproject ->
    apply(plugin: "java")
    apply(plugin: "osgi")

    apply(from: rootProject.file("gradle/checkstyle.gradle"))
    if (!usingJava9) {
        // throws an IllegalArgumentException on Java 9
        apply(from: rootProject.file("gradle/findbugs.gradle"))
    }
    apply(from: rootProject.file("gradle/jacoco.gradle"))
    //apply(from: rootProject.file("gradle/jdepend.gradle"))
    apply(from: rootProject.file("gradle/license.gradle"))

    apply(from: rootProject.file("gradle/publish-jar.gradle"))
    apply(from: rootProject.file("gradle/publish-maven.gradle"))
    apply(from: rootProject.file("gradle/publish-bintray.gradle"))
    apply(from: rootProject.file("gradle/travis-helpers.gradle"))

    sourceSets.main.compileClasspath += configurations.providedCompile
    sourceSets.test.compileClasspath += configurations.providedCompile
    sourceSets.test.runtimeClasspath += configurations.providedCompile

    task("sourceJar", type: Jar) {
        group "Build"
        description "An archive of the source code"
        classifier "sources"
        from sourceSets.main.allJava
    }

    task("javadocJar", type: Jar) {
        group "Build"
        description "An archive of the javadoc"
        classifier "javadoc"
        from javadoc
    }

    jar.finalizedBy sourceJar
    jar.finalizedBy javadocJar

    artifacts {
        sourceJar
        javadocJar
    }

    subproject.tasks.withType(JavaCompile) {
        sourceCompatibility = javaSourceAndTargetCompilation
        targetCompatibility = javaSourceAndTargetCompilation
    }

    // configure javadoc task.
    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption("Xdoclint:none", "-quiet")
        }
    }

    // use utf-8 encoding for java compile.
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    test {
        reports.junitXml.destination = file("$projectDir/../../build/test-reports/xml")
        testLogging {
            // Fancy formatting from http://stackoverflow.com/a/36130467/3634630
            // set options for log level LIFECYCLE
            events TestLogEvent.PASSED, TestLogEvent.SKIPPED,
                TestLogEvent.FAILED, TestLogEvent.STANDARD_OUT
            showExceptions true
            exceptionFormat TestExceptionFormat.FULL
            showCauses true
            showStackTraces true

            // set options for log level DEBUG and INFO
            debug {
                events TestLogEvent.STARTED, TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED, TestLogEvent.FAILED,
                    TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR
            }
            info.events = debug.events
            def tuple
            beforeSuite { desc ->
                if (!desc.parent) { // will match the outermost suite
                    travisFoldStart(desc.name)
                    tuple = travisTimerStart()
                }
            }
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    if (tuple != null) {
                        travisTimerEnd(tuple.first, tuple.second)
                    }
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                    travisFoldEnd(desc.name)
                }
            }
        }
    }
}

evaluationDependsOnChildren()

subprojects { subproject ->
    // maven repositories for dependencies.
    repositories {
        if (project.hasProperty("useMavenLocal")) {
            mavenLocal()
        }
        jcenter()
    }

    // configure javadoc task.
    javadoc {
        excludes = ["**/*.html", "META-INF/**"]
        classpath = configurations.compile + configurations.providedCompile + configurations.compileOnly
        options.use         = true
        options.splitIndex  = true
        options.encoding    = "UTF-8"
        options.author      = true
        options.version     = subproject.sourceCompatibility
        options.windowTitle = "${subproject.name} ${version} API"
        options.docTitle    = "${subproject.name} ${version} API"
        options.footer      = project.javadocFooter
        options.links       = javadocLinks
        options.noTimestamp = true
    }
}

evaluationDependsOnChildren()

task("aggregateJavadoc", type: Javadoc) {
    group "Documentation"
    description "Generates aggregated Javadoc API documentation."

    def javadocSubprojects = subprojects.findAll { project ->
        project.sourceSets.main.allJava.matching { include "org/testfx/**" }.with { !it.empty }
    }

    source javadocSubprojects.collect { project ->
        project.sourceSets.main.allJava
    }
    classpath = files(javadocSubprojects.collect { project ->
        project.sourceSets.main.compileClasspath
    })

    title "${project.javadocName} ${project.version} API"
    destinationDir = new File(projectDir, "docs/javadoc")

    configure(options) {
        use = true
        splitIndex = true
        encoding = "UTF-8"
        links = javadocLinks
    }

    // disable java 8 overly pedantic lint checking.
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption("Xdoclint:none", "-quiet")
    }

    doFirst {
        project.delete(destinationDir)
        logger.info "Title    : ${options.windowTitle}"
        logger.info "Destdir  : ${destinationDir}"
    }
}
